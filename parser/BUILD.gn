# Copyright 2023 The Toucan Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

action("run_bison") {
  script = "../tools/run.py"
  sources = [ "parser.yy" ]
  outputs = [
    "$target_gen_dir/parser.tab.cc",
    "$target_gen_dir/parser.tab.hh",
  ]
  if (is_win) {
    args = [ rebase_path("//third_party/winflexbison/win_bison.exe") ]
  } else {
    args = [ "bison" ]
  }
  args += [
    "-v",
    "-d",
    "-b", rebase_path(target_gen_dir, root_build_dir) + "/parser",
  ] + rebase_path(sources, root_build_dir)
}

action("run_flex") {
  script = "../tools/run.py"
  sources = [ "lexer.ll" ]
  outputs = [ "$target_gen_dir/lexer.cc" ]
  if (is_win) {
    args = [ rebase_path("//third_party/winflexbison/win_flex.exe") ]
  } else {
    args = [ "flex" ]
  }
  args += [
    "-o" + rebase_path(target_gen_dir, root_build_dir) + "/lexer.cc",
  ] + rebase_path(sources, root_build_dir)
}

source_set("parser") {
  configs += [ "../gn:disable-flex-warnings" ]
  sources = [
    "$target_gen_dir/parser.tab.cc",
    "$target_gen_dir/lexer.cc",
  ]
  deps = [
    ":run_bison",
    ":run_flex",
  ]
  include_dirs = [
    "..",
    "$target_gen_dir",
  ]
  if (is_win) {
    include_dirs += [ "C:\Program Files (x86)\GnuWin32\include" ]
  }
}
