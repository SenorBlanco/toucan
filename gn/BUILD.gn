# Copyright 2023 The Toucan Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

config("default") {
  if (is_win) {
    include_dirs = [
      "$win_vc/Tools/MSVC/$win_toolchain_version/include",
      "$win_sdk/Include/$win_sdk_version/shared",
      "$win_sdk/Include/$win_sdk_version/ucrt",
      "$win_sdk/Include/$win_sdk_version/um",
      "$win_sdk/Include/$win_sdk_version/winrt",
      "C:\Program Files (x86)\GnuWin32\include",
    ]
    lib_dirs = [
      "$win_sdk/Lib/$win_sdk_version/ucrt/$current_cpu",
      "$win_sdk/Lib/$win_sdk_version/um/$current_cpu",
      "$win_vc/Tools/MSVC/$win_toolchain_version/lib/$current_cpu",
    ]
    if (is_debug) {
      cflags = [ "/MDd" ]
    } else {
      cflags = [ "/MD" ]
    }
    cflags_cc = [ "/std:c++17" ]
    defines = [
      "_HAS_EXCEPTIONS=0",
      "NOMINMAX",
    ]
    ldflags = [ "/DEBUG:FASTLINK" ]
  } else {
    cflags_cc = [ "-std=c++17" ]
    cflags_objcc = [ "-std=c++17" ]
    defines = []
    if (is_linux) {
      cflags = [ "-fPIC" ]
    }
  }
  if (!is_debug) {
    defines += [ "NDEBUG" ]
  }
  if (target_os == "wasm") {
    defines += [ "TARGET_IS_WASM=1" ]
  } else if (target_os == "mac") {
    defines += [ "TARGET_IS_MAC=1" ]
  }
}

config("debug_symbols") {
  if (is_win) {
    cflags = [ "/Z7"]
    ldflags = [ "/DEBUG:FASTLINK" ]
  } else {
    cflags = [ "-g" ]
  }
}

config("disable-flex-warnings") {
  if (!is_win) {
    cflags = [ "-Wno-deprecated-register" ]
  }
}
